import net.neoforged.gradleutils.PomUtilsExtension

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'net.neoforged.gradleutils'
}

group = 'net.neoforged.jst'

gradleutils {
    setupSigning(project: project, signAllPublications: true)
}
java {
    withSourcesJar()
    withJavadocJar()
}

dependencies {
    api "com.jetbrains.intellij.java:java-psi-impl:$intellij_version"
    api "info.picocli:picocli:$picocli_version"
    api "net.neoforged.installertools:problems-api:$problems_api_version"
    compileOnly "org.jetbrains:annotations:$jetbrains_annotations_version"
}

publishing {
    publications {
        api(MavenPublication) {
            artifactId = 'jst-api'

            from components.java
            pom {
                name = 'JST API'
                description = 'The JavaSourceTransformer API'
                rootProject.pomUtils.githubRepo(it, 'JavaSourceTransformer')
                rootProject.pomUtils.neoForgedDeveloper(it)
                rootProject.pomUtils.license(it, PomUtilsExtension.License.LGPL_v2)
            }
        }
    }
    repositories {
        maven gradleutils.publishingMaven
    }
}

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow'
    id 'maven-publish'
    id 'net.neoforged.gradleutils'
}

group = 'net.neoforged.jst'

jar {
    manifest {
        attributes 'Main-Class': 'net.neoforged.jst.cli.Main'
    }
}

gradleutils.setupSigning(project: project, signAllPublications: true)

configurations {
    shadow
    shadow.extendsFrom(implementation)
}

dependencies {
    implementation project(":api")
    implementation "info.picocli:picocli:$picocli_version"
    implementation 'org.slf4j:slf4j-simple:2.0.13'

    shadow project(":parchment")
    shadow project(":accesstransformers")
    shadow project(':interfaceinjection')

    testImplementation platform("org.junit:junit-bom:$junit_version")
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation "org.assertj:assertj-core:$assertj_version"
}

test {
    useJUnitPlatform()
}

shadowJar {
    configurations = [project.configurations.shadow]
    mergeServiceFiles()
}

assemble.configure {
    dependsOn shadowJar
}

// This skips the shadowjar from being published as part of the normal publication
components.java.withVariantsFromConfiguration(configurations.shadowRuntimeElements) {
    skip()
}

publishing {
    publications {
        // This publication only contains the unshaded jar with dependencies in the pom.xml
        plain(MavenPublication) {
            artifactId = 'jst-cli'

            from components.java
        }
        // This publication only contains the shaded standalone jar
        bundle(MavenPublication) {
            artifactId = 'jst-cli-bundle'

            project.shadow.component(bundle)
        }
    }
    repositories {
        maven gradleutils.publishingMaven
    }
}
